// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.kotlin_version = "1.3.72"

    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:4.0.0"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

plugins {
    id 'project-report'
    id 'org.openapi.generator' version '4.3.1'
}

allprojects {
    repositories {
        maven { url "https://jitpack.io" }
        google()
        jcenter()
    }
}

htmlDependencyReport {
    projects = project.allprojects
}

ext {
    packageName = "au.com.woolworths.village.sdk.openapi"
    spec = "$projectDir/village.json".toString()

    // what version of the spec does the implementation support
    supportedSpecVersion = "0.0.6"

    // to support a new version of the library, even if the spec hasn't changed.
    buildNumber = 0

    minSdkVersion = 21
    targetSdkVersion = 28
    buildToolsVersion = "29.0.3"
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

openApiValidate {
    inputSpec = project.ext.spec
}

def pascalCase = {
    return it
        .split("-")
        .collect({ s -> "${Character.toUpperCase(s.charAt(0))}${s.substring(1).toLowerCase()}" })
        .join("")
}

def camelCase = { it ->
    def pc = pascalCase(it)
    return "${pc[0].toLowerCase()}${pc.substring(1)}"
}

def clients = [
    "okhttp-gson": [
        language: "java",
        library: "okhttp-gson"
    ],
    "objc": [
        language: "objc",
        library: null
    ],
    "javascript": [
        language: "javascript",
        library: null
    ]
]

clients.each { name, client ->
    def outputLoc = "${projectDir}/${name}".toString()

    task("generate${pascalCase(name)}SdkApi", type: org.openapitools.generator.gradle.plugin.tasks.GenerateTask) {
        group = "SDK API Generators"
        description = "Generates a Village SDK API client using the ${name} framework"
        generatorName = client.language
        validateSpec = true
        library = client.library
        skipOverwrite = false

        inputSpec = project.ext.spec
        outputDir = outputLoc
        version = "${project.ext.supportedSpecVersion}.${project.ext.buildNumber}"

        switch (client.language) {
            case "java":
                groupId = project.ext.packageName
                id = "${name}"
                packageName = "${project.ext.packageName}"
                apiPackage = "${packageName.get()}.api"
                invokerPackage = "${packageName.get()}.client"
                modelPackage = "${packageName.get()}.dto"

                break

            case "objc":
                additionalProperties = [
                  podName: "VillageOpenApiClient",
                  podVersion: "${version.get()}"
                ]

                break

            case "javascript":
                apiPackage = "api"
                invokerPackage = "client"
                modelPackage = "dto"

                additionalProperties = [
                    usePromises: "true"
                ]
        }

        generateModelTests = false
        generateModelDocumentation = false
        generateApiTests = false

        configOptions = [
            hideGenerationTimestamp: "true",
            "serializableModel": "true"
        ]
    }
}

def sdkTestsProject = null
def evaluatedProjects = []

def createApiFlavor = { prj ->
    logger.info("Creating SDK flavor for ${prj.path}")

    project.configure(sdkTestsProject, {
        android {
            productFlavors.create("${camelCase(prj.name)}", {})
        }

        /*
         * Add the SDK API project as a dependency for the flavour testing
         */
        dependencies {
            "test${pascalCase(prj.name)}Implementation" prj
        }
    })
}

/*
 * Configure the SDK API tests project to have a flavour for each API variant.
 *
 * See the README for further explanation.
 */
subprojects.each { sub ->
    /*
     * The project configuration eg: the "android" block has to be evaluated first so that we can
     * manipulate it.
     */
    sub.afterEvaluate { prj ->
        // we don't care about other projects except API/tests
        if (!clients.containsKey(prj.name) && prj.name != "sdk-tests") {
            logger.info("Ignoring ${prj.name}")

            return
        }

        /*
         * Because we don't know the order Gradle will evaluate (load) sub projects we can't guarantee
         * that the SDK API tests project will have been evaluated when other sub projects are. What we
         * therefore do is:
         *
         * 1. If the SDK tests project has been evaluated, create a flavour for the newly evaluated project.
         * 2. If not (1) save the project for later.
         * 3. If the newly evaluated project is the SDK tests, create a flavour for any previously saved projects.
         */
        logger.debug("${prj.path} is evaluated")

        if (prj.path == ":sdk-tests") {
            logger.debug("${prj.path} is evaluated; creating flavours for any saved projects")
            sdkTestsProject = prj

            evaluatedProjects.each(createApiFlavor)
        }
        else {
            if (sdkTestsProject != null) {
                createApiFlavor(prj)
            }
            else {
                logger.debug("Saving ${prj.path} for later configuration")

                evaluatedProjects.add(prj)
            }
        }
    }
}